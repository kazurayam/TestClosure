// TestClosure/build.gradle

// $ gradle drivers


plugins {
  id 'groovy'
}

group = 'com.kazurayam'
version = '0.23.0'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    groovyVersion = '2.4.21'
    chromedriverfactoryVersion = '0.6.6'
    ashotVersion = '1.5.4'
    ashotwrapperVersion = '0.1.0'
    junit4ksVersion = '1.6.1'
    timekeeperVersion = '0.3.3'
    browserwindowlayoutVersion = '0.1.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
    generateDocs
}

dependencies {
    myconf group: 'ru.yandex.qatools.ashot', name: 'ashot', version: "${ashotVersion}"
    myconf group: 'com.kazurayam', name: 'chromedriverfactory', version: "${chromedriverfactoryVersion}"
    myconf group: 'com.kazurayam', name: 'ashotwrapper', version: "${ashotwrapperVersion}"
    myconf group: 'com.kazurayam', name: 'junit4ks', version: "${junit4ksVersion}"
    myconf group: 'com.kazurayam', name: 'timekeeper', version: "${timekeeperVersion}"
    myconf group: 'com.kazurayam', name: 'browserwindowlayout', version: "${browserwindowlayoutVersion}"
    generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/ashot*.jar",
                    "**/ashotwrapper*.jar",
                    "**/junit4ks*.jar",
                    "**/timekeeper*.jar",
                    "**/chromedriverfactory*.jar",
                    "**/browserwindowlayout*.jar"
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}




def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each {it.groovyOptions.encoding = defaultEncoding }

sourceSets {
    main {
        groovy {
            srcDirs = ['Keywords']
        }
    }
}

task groovydoc(type:Groovydoc, overwrite:true) {
    group = 'com.kazurayam'
    source = sourceSets.main.groovy
    classpath = configurations.implementation
    groovyClasspath = project.configurations.generateDocs
    include 'com/kazurayam/ks/globalvariable/*'
    exclude '**/*Test.groovy'
}

task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}

groovydoc.finalizedBy publishGroovydoc
